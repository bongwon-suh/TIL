# LED

### 시리얼 모니터를 통한 LED 제어

```c++
int pins[] = {13, 12, 11, 10};
int state = 0;
void setup()
{
    Serial.begin(9600);
    for (int i = 0; i < 4; i++)
    {
        pinMode(pins[i], OUTPUT);
        digitalWrite(pins[i], LOW);
    }
}
void loop()
{
    if (Serial.available())
    {
        char data = Serial.read();
        if (data == '\r' || data == '\n')
            return;
        Serial.println(String("You entered \'") + data + '\'');
        if (data >= '1' && data <= '4'){
            state = data - '0' - 1; // LED 인덱스로 변환
            Serial.print("LED ");
            Serial.print(state + 1);
            Serial.println("i On...");
        }
        else{
            Serial.println("* Invalid LED number ...");
            state =-1;
        }
        for (int i = 0;i < 4;i++)
        {
            if (i == state){
                Serial.print("O ");
                digitalWrite(pins[i], HIGH);
            }
            else{
                Serial.print("X ");
                digitalWrite(pins[i], LOW);
            }
        }
        Serial.println();
    }
}
```



### 시간 측정 함수

- millis()

  - 현재 시간을 ms로 리턴
  - unsigned long 타입으로 리턴

  ```c++
  int pin_LED1 = 13;
  boolean LED_state1 = false;
  
  int pin_LED2 = 12;
  boolean LED_state2 = false;
  
  void setup()
  {
      pinMode(pin_LED1, OUTPUT);
      pinMode(pin_LED2, OUTPUT);
  
      digitalWrite(pin_LED1, LED_state1);
      digitalWrite(pin_LED2, LED_state2);
  }
  
  void loop()
  {
  
  blink_led1();
  blink_led2();
  
  }
  
  void blink_led1()
  {
      static unsigned long time_previous = 0;
      const int INTERVAL = 500;
      unsigned long time_current = millis();
      if (time_current - time_previous > INTERVAL)
      {
          time_previous = time_current;
          LED_state1 = !LED_state1;
          digitalWrite(pin_LED1, LED_state1);
      }
  }
  void blink_led2()
  {
      static unsigned long time_previous = 0;
      const int INTERVAL = 1000;
      unsigned long time_current = millis();
      if (time_current - time_previous > INTERVAL)
      {
          time_previous = time_current;
          LED_state2 = !LED_state2;
          digitalWrite(pin_LED2, LED_state2);
      }
  }
  ```



- SimpleTimer 라이브러리

  - 주요 메소드
    - `int setInterval(long d, timer_callback f)`
      d 밀리초마다 함수 f를 호출. 콜백 함수는 void f() 형식으로 선언되어야 함
    - `int setTimeout(long d, timer_callback f)`
      d 밀리초 이후에 함수 f를 한번만 호출
    - `int setTimer(long d, timer_callback f, int n)`
      d 밀리초 마다 정한 숫자만큼 함수 f를 호출함.
    - `boolean isEnabled(int timerId)`
      지정된 타이머가 활성화가 되면 true를 반환
    - `void enable(int timerId)`
      지정한 타이머를 활성화.
    - `void disable(int timerId)`
      지정한 timerId 비활성화
    - `void toggle(int timerId)`
      타이머 상태 토클(활성화/비활성화)
    - `void restartTimer(int timerId)`
      지정한 타이머를 다시 시작
    - `void deleteTimer(int timerId)`
      지정한 timerId 삭제
    - `void getNumTimers()`
      실행 중인 타이머 개수를 변수 n에 저장

  ```c++
  #include <SimpleTimer.h>
  int pin_LED1 = 13;
  int pin_LED2 = 12;
  
  SimpleTimer timer;
  
  void blink_1000(){
      int state = digitalRead(pin_LED1);  // 지정한 핀의 현재 상태 읽기
      digitalWrite(pin_LED1, !state);
  }
  
  void blink_500(){
      int state = digitalRead(pin_LED2);
      digitalWrite(pin_LED2, !state);
  }
  
  void setup() {
      pinMode(pin_LED1, OUTPUT);
      pinMode(pin_LED2, OUTPUT);
      timer.setInterval(1000, blink_1000);
      timer.setInterval(500, blink_500);
  }
  
  void loop(){
      timer.run();
  }
  ```



### LED 밝기 조절(아날로그 출력)

- 아날로그 출력 방식

  - 디지털 출력을 조절해서 동일한 효과를 구현
  - PWM(Pulse Width Modulation) : 펄스 폭 변조
    - 디지털 값 -> 아날로그 값 : 아날로그 출력
    - 아날로그 값의 비율 만큼 HIGH 폭을 가변
    - PWM 주파수 : 500Hz

  ![](https://github.com/bongwon-suh/TIL/blob/master/img/0911_1.JPG?raw=true)

- Duty rate, Duty Cycle

  - 500Hz -> 2ms마다 바뀜
  - `analogWrite()`는 0~255 값 출력
    - `analogWrite(255)` : 항상 켜짐, 100%
    - `analogWrite(127)`: 50 %

![](https://github.com/bongwon-suh/TIL/blob/master/img/0911_2.JPG?raw=true)



- analogWrite()
  - 아날로그 출력 핀의 전압을 설정하는 함수
  - 전압을 0V에서 5V사이에 원하는 값으로 설정
  - ~ 표시가 있는 핀을 사용(6개)
    - ~3, ~5, ~6, ~9, ~10, ~11
  - `analogWrite(핀 번호, 전압)`
    - 매개 변수
      - 핀 번호 : 핀 번호 : 전압을 설정하고자 하는 핀 번호
      - 전압 : 0~255. 0V는 0, 5V는 255를 입력
    - 반환값 : 없음

```c++
int pin_LED = 11;

void setup() {
    pinMode(pin_LED, OUTPUT);
}

void loop(){
    for(int i=0;i<255;i++){
        analogWrite(pin_LED, i);
        delay(20);
    }

    for(int i=255; i>=0; i--){
        analogWrite(pin_LED,i);
        delay(20);
    }
}
```





# 3색 LED

### 3색 LED

- R G B 핀에 0~255 사이의 값을 `analogWrite()` 함수로 출력
- 3색의 조합으로 색상 결정

![](https://github.com/bongwon-suh/TIL/blob/master/img/0911_3.JPG?raw=true)

```c++
const int redPin = 11;
const int greenPin = 10;
const int bluePin = 9;

void setup() {
    randomSeed(analogRead(A0));
}

void loop() {
    analogWrite(redPin, random(256));
    analogWrite(greenPin, random(256));
    analogWrite(bluePin, random(256));
    delay(1000);
}
```

